10 PCLEAR 8 : DIM X(25,25),Y(25,25),X2(25,25),Y2(25,25)
20 CLS:PRINT@6,"FAULT LINE MACHINE":PRINT:PRINT"G....GENERATE FAULT LINES.":PRINT"C....CONTINUE FAULT GENERATION.":PRINT"S....SAVE LAND MASS DATA.":PRINT"L....LOAD LAND MASS DATA.":PRINT"F....FILL IN SEAS."
30 PRINT "R....REMOVE SEAS.":PRINT"D....SAVE DISPLAY.":PRINT"V....VERTICAL OFFSET."
40 A$=INKEY$:IF A$="" THEN A=RND(-TIMER):GOTO 40
50 IF A$="M" THEN 20
60 IF A$="G" THEN 160
70 IF A$="C" THEN PMODE4,5:INPUT"NUMBER OF EXTRA FAULTS";Q:GOTO280
80 IF A$="S" THEN 970
90 IF A$="L" THEN 1060
100 IF A$="F" THEN 860
110 IF A$="R" THEN 940
120 IF A$="V" THEN 830
150 GOTO 20
160 POKE65495,0
170 A=RND(-TIMER)
180 CLS:PRINT@265,"FAULT LINE MACHINE":PRINT
190 PMODE4,5:PCLS
200 INPUT"SIZE OF GRID";SIZE
210 INPUT"NUMBER OF FAULT LINES";Q
220 FOR A=0 TO SIZE:FOR B=0 TO SIZE
230 V=120/(SIZE)
240 H=2*230/(SIZE*3)
250 Y(A,B)=150-B*V
260 X(A,B)=10+A*H+B*H/2
270 NEXT B,A
280 REM SCREEN 1,0
290 FOR QUAKES=1 TO Q
300 X1 = RND(SIZE-1):X2=RND(SIZE-1):Y1=RND(SIZE-1):Y2=RND(SIZE-1)
310 IF X1=X2 THEN 300
320 IF Y1=Y2 THEN 300
330 IF X1=X2 THEN 300
340 'EQUATION = AX+B=Y
350 A=(Y1-Y2)/(X1-X2)
360 B=Y1-(A*X1)
370 'UP OR DOWN
380 C=RND(2):IF C=2 THEN CONST=-3 ELSE CONST=+3
390 'CALCULATE EARTHQUAKE
400 FOR W=1 TO SIZE-1
410 FOR Z=1 TO SIZE-1
420 IF A*W+B<Z THEN Y(W,Z) = Y(W,Z) + CONST ELSE Y(W,Z)=Y(W,Z) - CONST
430 NEXT Z,W
440 'DRAW QUAKE ZONE
445 SCREEN 1,0
450 PMODE4,5:PCLS
460 FOR Z=SIZE TO 1 STEP -1
470 FOR W=1 TO SIZE
480 LINE(X(W-1,Z),Y(W-1,Z))-(X(W,Z),Y(W,Z)),PSET
490 LINE-(X(W,Z-1),Y(W,Z-1)),PSET
500 NEXT W,Z
510 LINE(X(0,SIZE),Y(0,SIZE))-(X(0,0),Y(0,0)),PSET
520 LINE-(X(SIZE,0),Y(SIZE,0)),PSET
530 FORA=1TO4:PCOPYA+4 TO A:NEXT A
540 PMODE4,1:SCREEN1,0
550 NEXT QUAKE
560 PMODE4,1:SCREEN1,1:GOTO40
580 'DRAW IN AS 3D SOLID
590 FOR Z=SIZE TO 1 STEP-1
600 FOR W=1 TO SIZE
610 X1=X2(W-1,Z):X2=X2(W,Z):X3=X2(W,Z-1):X4=X2(W-1,Z-1)
620 Y1=Y2(W-1,Z):Y2=Y2(W,Z):Y2=Y2(W,Z):Y3=Y2(W,Z-1):Y4=Y2(W-1,Z-1) 
630 A1=(Y1-Y2)/(X1-X2):B1=Y1-(A1*X1)
640 IF K=1 THEN IF Y1=Y2 AND Y1=150-Z*V THEN COLOR 0,1 ELSE COLOR1,0
650 FOR X=X1 TO X2
660 Y=A1*X+B1
670 LINE(X,Y)-(X3,Y3),PRESET
680 NEXT X
690 A1=(Y4-Y3)/(X4-X3):B1=Y4-(A1*X4)
700 FOR X=X4 TO X3
710 Y=A1*X+B1
720 LINE(X1,Y1)-(X,Y),PRESET
730 NEXT X
740 LINE(X1,Y1)-(X2,Y2),PSET:LINE-(X3,Y3),PSET:LINE-(X4,Y4),PSET:LINE-(X1,Y1),PSET
750 NEXT W,Z
760 COLOR1,0
770 LINE(X(0,0),Y(0,0))-(X(SIZE,0),Y(SIZE,0)),PSET:LINE-(X(SIZE,SIZE),Y(SIZE,SIZE)),PSET
780 POKE65494,0
790 SOUND 100,1
800 SCREEN 1,1
810 GOTO 40
820 'MOVE LANDMASS
830 PRINT:INPUT"ENTER LAND SHIFT OFFSET";A
840 FOR W=1 TO SIZE-1:FOR Z=1 TO SIZE-1:Y(W,Z)=Y(W,Z)-A:NEXTZ,W:GOTO20
850 'FILL IN THE SEA
860 SCREEN1,0:FOR W=0 TO SIZE
870 FOR Z=0 TO SIZE
880 IF Y(W,Z)>150-Z*V THEN Y2(W,Z)=150-Z*V ELSE Y2(W,Z)=Y(W,Z)
890 X2(W,Z) = X(W,Z)
900 NEXTZ,W
910 K=1
920 PMODE4,1:PCLS:SCREEN1,0:GOTO590:'DRAW LAND MASS
930 'REMOVE SEA
940 SCREEN1,0:FOR W=0 TO SIZE:FOR Z=0 TO SIZE
950 Y2(W,Z)=Y(W,Z):X2(W,Z)=X(W,Z):NEXTZ,W:K=0
960 PMODE4,1:PCLS:SCREEN1,0:GOTO590
970 CLS:PRINT@230,"SAVE LANDSCAPE DATA":PRINT:PRINT
980 INPUT "FILE TITLE";FILE$
990 FWRITE FILE$;SIZE
1000 FOR A=0 TO SIZE:FOR B=0 TO SIZE
1010 FWRITE FILE$;X(A,B)
1020 FWRITE FILE$;Y(A,B)
1030 NEXT B,A
1040 CLOSE
1050 GOTO 20
1060 CLS:PRINT@230,"LOAD LANDSCAPE DATA":PRINT:PRINT
1070 INPUT "ENTER FILE TITLE";FILE$
1080 FREAD FILE$;SIZE
1090 FOR A=0 TO SIZE:FOR B=0 TO SIZE
1100 FREAD FILE$;X(A,B)
1110 FREAD FILE$;Y(A,B)
1120 NEXT B,A
1130 CLOSE
1140 V=120/SIZE
1150 GOTO 20
1160 PRINT: INPUT"SAVE DISPLAY TITLE";A$
1170 SAVE A$,3072,9216,41194:GOTO20
1180 '(C)1986 BY PETER WHITTAKER
1190 ' OCTOBER 1986 DRAGON USER