10 PCLEAR4:DIM X(25,25),Y(25,25),X2(25,25),Y2(25,25):D=0:W=0:Z=0:A=0:B=0:H=0:S=0:V=0:X1=0:X2=0:X3=0:X4=0:Y1=0:Y2=0:Y3=0:Y4=0:A1=0:B1=0
20 CLS:?@6,"FAULT LINE MACHINE":?:?"G....GENERATE FAULT LINES.":?"C....CONTINUE FAULT GENERATION.":?"S....SAVE LAND MASS DATA.":?"L....LOAD LAND MASS DATA.":?"F....FILL IN SEAS."
30 ?"R....REMOVE SEAS.":?"V....VERTICAL OFFSET.":?"A....RANDOMISE."
40 A$=INKEY$:IF A$="" THEN A=RND(-TIMER):GOTO 40
50 IF A$="M" THEN 20
60 IF A$="G" THEN 160
70 IF A$="C" THEN PMODE4,1:INPUT"NUMBER OF EXTRA FAULTS";Q:GOTO280
80 IF A$="S" THEN 970
90 IF A$="L" THEN 1060
100 IF A$="F" THEN 860
110 IF A$="R" THEN 940
120 IF A$="V" THEN 830
130 IF A$="A" THEN 1200
150 GOTO 20
160 POKE65495,0
170 A=RND(-TIMER)
180 CLS:?@265,"FAULT LINE MACHINE":?
190 PMODE4,1:PCLS
200 INPUT"SIZE OF GRID";SIZE
210 INPUT"NUMBER OF FAULT LINES";Q
220 FOR A=0 TO SIZE:FOR B=0 TO SIZE
230 V=120/(SIZE)
240 H=2*230/(SIZE*3)
250 Y(A,B)=150-B*V
260 X(A,B)=10+A*H+B*H/2
270 NEXT:?@480+A,"*";:NEXT
280 CLS:SCREEN 1,0
290 FOR QUAKES=1 TO Q
300 X1 = RND(SIZE-1):X2=RND(SIZE-1):Y1=RND(SIZE-1):Y2=RND(SIZE-1)
310 IF X1=X2 THEN 300
320 IF Y1=Y2 THEN 300
350 A=(Y1-Y2)/(X1-X2)
360 B=Y1-(A*X1)
380 C=RND(2):IF C=2 THEN D=-3 ELSE D=+3
400FORW=1TOSIZE-1:FORZ=1TOSIZE-1
420IFA*W+B<Z THENY(W,Z)=Y(W,Z)+D ELSE Y(W,Z)=Y(W,Z)-D
430NEXT:NEXT
445 SCREEN 1,0
450 PMODE4,1:PCLS
455 IF Q>0 THENLINE(0,1)-((255/Q)*QUAKES,1),PSET
460 FOR Z=SIZE TO 1 STEP -1:FOR W=1 TO SIZE:LINE(X(W-1,Z),Y(W-1,Z))-(X(W,Z),Y(W,Z)),PSET:LINE-(X(W,Z-1),Y(W,Z-1)),PSET:NEXT:NEXT
510 LINE(X(0,SIZE),Y(0,SIZE))-(X(0,0),Y(0,0)),PSET
520 LINE-(X(SIZE,0),Y(SIZE,0)),PSET
540 PMODE4,1:SCREEN1,0
550 NEXT QUAKE
560 PMODE4,1:SCREEN1,1:LINE(0,1)-(255,1),PRESET:GOTO40
590 FOR Z=SIZE TO 1 STEP-1
600 FOR W=1 TO SIZE
610 X1=X2(W-1,Z):X2=X2(W,Z):X3=X2(W,Z-1):X4=X2(W-1,Z-1)
620 Y1=Y2(W-1,Z):Y2=Y2(W,Z):Y2=Y2(W,Z):Y3=Y2(W,Z-1):Y4=Y2(W-1,Z-1) 
630 A1=(Y1-Y2)/(X1-X2):B1=Y1-(A1*X1)
640 IF K=1 THEN IF Y1=Y2 AND Y1=150-Z*V THEN COLOR 0,1 ELSE COLOR1,0
650FORX=X1 TO X2:Y=A1*X+B1:LINE(X,Y)-(X3,Y3),PRESET:NEXT:A1=(Y4-Y3)/(X4-X3):B1=Y4-(A1*X4):FORX=X4 TO X3:Y=A1*X+B1:LINE(X1,Y1)-(X,Y),PRESET:NEXT
740LINE(X1,Y1)-(X2,Y2),PSET:LINE-(X3,Y3),PSET:LINE-(X4,Y4),PSET:LINE-(X1,Y1),PSET
750NEXT:NEXT
760 COLOR1,0
770 LINE(X(0,0),Y(0,0))-(X(SIZE,0),Y(SIZE,0)),PSET:LINE-(X(SIZE,SIZE),Y(SIZE,SIZE)),PSET
780 POKE65494,0
790 SOUND 100,1
800 SCREEN 1,1
810 GOTO 40
830 ?:INPUT"ENTER LAND SHIFT OFFSET";A
840 FOR W=1 TO SIZE-1:FOR Z=1 TO SIZE-1:Y(W,Z)=Y(W,Z)-A:NEXTZ,W:GOTO20
860 SCREEN1,0:FOR W=0 TO SIZE
870 FOR Z=0 TO SIZE
880 IF Y(W,Z)>150-Z*V THEN Y2(W,Z)=150-Z*V ELSE Y2(W,Z)=Y(W,Z)
890 X2(W,Z) = X(W,Z)
900 NEXT:NEXT
910 K=1
920 PMODE4,1:PCLS:SCREEN1,0:GOTO590:'DRAW LAND MASS
940 SCREEN1,0:FOR W=0 TO SIZE:FOR Z=0 TO SIZE
950 Y2(W,Z)=Y(W,Z):X2(W,Z)=X(W,Z):NEXTZ,W:K=0
960 PMODE4,1:PCLS:SCREEN1,0:GOTO590
970 CLS:?@230,"SAVE LANDSCAPE DATA":?:?
980 INPUT "FILE TITLE";FILE$
990 FWRITE FILE$;SIZE
1000 FOR A=0 TO SIZE:FOR B=0 TO SIZE
1010 FWRITE FILE$;X(A,B)
1020 FWRITE FILE$;Y(A,B)
1030 NEXT B,A
1040 CLOSE
1050 GOTO 20
1060 CLS:?@230,"LOAD LANDSCAPE DATA":?:?
1070 INPUT "ENTER FILE TITLE";FILE$
1080 FREAD FILE$;SIZE
1090 FOR A=0 TO SIZE:FOR B=0 TO SIZE
1100 FREAD FILE$;X(A,B)
1110 FREAD FILE$;Y(A,B)
1120 NEXT B,A
1130 CLOSE
1140 V=120/SIZE
1150 GOTO 20
1200 FOR A=1 TO SIZE-1:?@480+A,"*";
1210 FOR B=1 TO SIZE-1
1220 Y(A,B)=Y(A,B)+RND(4)-2
1230 NEXT B,A:SOUND255,1
1240 GOTO 20
