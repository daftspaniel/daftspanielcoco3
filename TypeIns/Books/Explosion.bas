10 REM TANDY-EXPLOSION 
15 POKE 65495,0
20 DIM RB(5,5), SB(5,5), ST(5,5) 
30 GOTO 2000 
40 FOR X=1 TO SI:FOR Y=1 TO SI:SB(X,Y)=RB(X, Y): NEXT Y,X: RETURN 
50 FOR X=1 TO SI:FOR Y=1 TO SI:RB(X,Y)=SB(X, Y): NEXT Y,X: RETURN 
100 REM**INITIALIZE** 
110 INPUT "ENTER SIZE OF BOARD (3 OR 4)";SI
120 IF SI<3 OR SI>4 THEN 110 
130 FOR X=1 TO SI 
140 FOR Y=1 TO SI 
150 ST(X,Y)=4+(X=1)+(X=SI)+(Y=1)+(Y=SI) 
160 NEXT Y,X 
170 INPUT"WHO STARTS? YOU (Y) OR ME (M):";AN$
180 CM=(LEFT$(AN$,1)="M") 
190 CLS 
200 PRINT @5, "X1 X2 X3" 
210 IF SI=4 THEN PRINT@14,"X4"
220 FOR Y=1 TO SI 
230 PRINT"   Y" + RIGHT$(STR$(Y),1);
240 FOR X=1 TO SI 
250 PRINT RB(X, Y); 
260 NEXT 
270 PRINT 
280 NEXT Y 
290 RETURN 
300 REM**INPUT AND EXECUTE MOVE** 
310 PRINT @160,; 
320 INPUT"X";MX 
330 MX=INT (MX) 
340 IF MX<1 OR MX>SI THEN 310 
350 INPUT"Y";MY 
360 MY=INT (MY) 
370 IF MY<1 OR MY>SI THEN 310
380 IF RB(MX, MY)<0 THEN 310 
390 GOSUB 40 
400 X=MX:Y=MY:DI=-1
410 GOSUB 500: REM OUTPUT EXPLOSION 
420 IF ET THEN 1200:REM END 
430 GOSUB 50 
440 RETURN 
500 REM**OUTPUT EXPLOSION** 
510 SB(X,Y)=SB(X,Y)+PL 
520 NE = 0 
530 IF DI THEN PRINT @2+3*X+32*Y, SB(X, Y); 
540 XP=0 
550 FOR X = 1 TO SI 
560 FOR Y = 1 TO SI 
570 IF ABS(SB(X,Y)) <ST(X,Y) THEN 660 
580 XP = - 1 
590 NE = NE + 1 
600 SB(X,Y)=SB(X,Y)-ST(X,Y)*PL 
610 IF DI THEN PRINT @2+32*Y+3*X, SB(X, Y);
620 EX=X:EY=Y+1: GOSUB 700 
630 EX=X+1:EY=Y: GOSUB 700 
640 EX=X:EY=Y-1: GOSUB 700 
650 EX=X-1:EY=Y: GOSUB 700 
660 NEXT Y, X 
670 ET=(NE>SI*SI) 
680 IF XP AND NOT ET THEN 540
690 RETURN 
700 REM**ADD TO NEIGHBOURS** 
710 SB(EX, EY)=PL*(ABS(SB(EX, EY))+1) 
720 IF DI AND ST(EX, EY)>0 THEN PRINT @2+32*EY+3*EX, SB(EX, EY);
730 RETURN 
800 REM**COMPUTER MOVES** 
810 BE=1000 
820 FOR TX = 1 TO SI 
830 FOR TY = 1 TO SI 
840 IF RB(TX, TY)>0 THEN 910 
850 GOSUB 40 
860 X=TX: Y=TY:DI=0 
870 GOSUB 500: REM OUTPUT EXPLOSION 
880 IF ET THEN MX=TX: MY=TY:GOTO 920 
890 GOSUB 1000: REM EVALUATE SITUATION
900 IF EN<BE OR EN=BE AND RND(10)<4 THEN BE=EN: MX=TX:MY=TY
910 NEXT TY, TX 
920 REM ACTUAL MOVE 
930 GOSUB 40 
935 REM PRINT @300, MX, MY
940 X=MX:Y=MY:DI=-1
950 GOSUB 500: REM OUTPUT EXPLOSION 
960 IF ET THEN 1200:REM END 
970 GOSUB 50 
980 RETURN 
1000 REM**EVALUATE SITUATION** 
1010 EN=0 
1020 FOR X = 1 TO SI 
1030 FOR Y = 1 TO SI 
1040 EN=EN+SB(X, Y) 
1050 IF -SB(X,Y)<ST(X, Y)-1 THEN 1110 
1060 EN=EN-2 
1070 IF SB(X + 1, Y) = ST(X + 1, Y) - 1 THEN EN= EN + 10
1080 IF SB(X, Y + 1) = ST(X, Y + 1) - 1 THEN EN=EN+10 
1090 IF SB(X - 1, Y) = ST(X - 1, Y) - 1 THEN EN= EN + 10 
1100 IF SB(X, Y - 1) = ST(X, Y - 1) - 1 THEN EN=EN+10 
1110 NEXT Y,X 
1120 RETURN
1200 REM**END** 
1210 PRINT @160, "AN ETERNAL EXPLOSION RESULTED, SO "; 
1220 IF PL = 1 THEN PRINT"YOU" ;ELSE PRINT"I"; 
1230 PRINT" HAVE WON IN"NT" TURNS" 
1240 END 
2000 REM**MAIN PROGRAM** 
2010 GOSUB 100: REM INITIALIZE 
2020 IF CM THEN 2040 
2030 PL = 1 : GOSUB 300: REM INPUT AND EXECUTE MOVE 
2040 PL = -1 : GOSUB 800: REM COMPUTER MOVES 
2050 NT = NT + 1 
2060 GOTO 2030
 